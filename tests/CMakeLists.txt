include_directories(${PROJECT_SOURCE_DIR}/include/eigen3)
include_directories(${PROJECT_SOURCE_DIR}/tests)

add_executable(interpolate.test EXCLUDE_FROM_ALL interpolate.test.cc)
target_link_libraries(interpolate.test libboys)
add_test(NAME interpolate COMMAND interpolate.test)

add_executable(boys.test EXCLUDE_FROM_ALL boys.test.cc)
target_link_libraries(boys.test libboys)
add_test(NAME boys COMMAND boys.test)

option(LIBINTX_LIBINT2 "" OFF)
if (LIBINTX_LIBINT2)
  find_package(Libint2 REQUIRED)
  add_library(libintx.test.reference libint2.reference.cc)
  set_target_properties(libintx.test.reference PROPERTIES OUTPUT_NAME intx.test.reference)
  target_compile_definitions(libintx.test.reference PUBLIC LIBINTX_TEST_REFERENCE)
  target_link_libraries(libintx.test.reference PRIVATE Libint2::cxx)
endif()

if (LIBINTX_CUDA)
  enable_language(CUDA)
  add_executable(boys.gpu.test EXCLUDE_FROM_ALL boys.gpu.test.cu)
  target_link_libraries(boys.gpu.test libboys libintx.gpu)
  add_test(NAME boys.gpu COMMAND boys.gpu.test)
endif()

foreach (n 3 4)
  if (NOT TARGET libintx.gpu.md${n})
    continue()
  endif()
  # tests
  add_executable(libintx.gpu.md${n}.test EXCLUDE_FROM_ALL libintx.gpu.md${n}.test.cc)
  target_link_libraries(libintx.gpu.md${n}.test libintx.gpu.md${n})
  # perf benchmarks
  add_executable(libintx.gpu.md${n}.perf EXCLUDE_FROM_ALL libintx.gpu.md${n}.perf.cc)
  target_link_libraries(libintx.gpu.md${n}.perf libintx.gpu.md${n})
  if (TARGET libintx.test.reference)
    target_link_libraries(libintx.gpu.md${n}.perf libintx.test.reference)
  endif()
endforeach()

add_executable(libintx.md.test EXCLUDE_FROM_ALL libintx.md.test.cc)
target_link_libraries(libintx.md.test libboys)
add_test(NAME libintx.md COMMAND libintx.md.test)
